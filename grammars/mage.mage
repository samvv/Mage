
# Should be part of standard library

any_byte = [\x00-\xFF]

any_char = [\x00-\x7F]

# Special rules

@skip
whitespace = [\n\r\t ]*

# Tokens

# A name in the language.
#
# ```
# foobar
# ```
#
# ```
# FooBar
# ```
#
# ```
# F2Bar
# ```
@keyword
pub token ident
  = [a-z_]i [a-z0-9_]i*

# An integral number.
#
# ```
# 1
# ```
#
# ```
# 5
# ```
#
# ```
# 45
# ```
#
# ```
# 0
# ```
pub token integer -> Integer
  = [0-9]+

hex_digit
  = [0-9a-f]i

escape_sequence
  = 'u' '{' hex_digit{4} '}'
  | 'x' hex_digit{2}
  | escaped_ascii_char

escaped_ascii_char
  = 'a'
  | 'b'
  | 'f'
  | 'n'
  | 'r'
  | 't'
  | 'v'
  | '\\'
  | '\''
  | '"'

# A single character in a character set expression.
#
# ```
# $
# ```
#
# ```
# a
# ```
#
# ```
# z
# ```
#
# ```
# _
# ```
#
# ```
# 0
# ```
#
# ```
# 9
# ```
#
# ```
# \x0A
# ```
single_char
  = '\\' escape_sequence
  | any_char

# A string literal.
#
# ```
# 'foo'
# ```
#
# ```
# 'Hello, world!'
# ```
#
# ```
# '$'
# ```
#
# ```
# '++'
# ```
pub token string
  = '\'' (!'\'' single_char)* '\''

mod CharSet {
  pub token eof = ']'
  pub token range = single_char '-' single_char
  pub token char = single_char
  pub element = range | char
  pub elements = (!eof element)*
}

# Expressions

pub expr
  = char_set_expr
  | choice_expr
  | list_expr
  | lit_expr
  | lookahead_expr
  | many_expr
  | negative_lookahead_expr
  | opt_expr
  | ref_expr
  | repeat_expr
  | seq_expr
  | some_expr

pub lit_expr
  = string

pub ref_expr
  = name:ident

pub char_set_expr
  = '~'? '[' CharSet.elements ']' 'i'?

pub seq_expr
  = elements:expr{2,}

pub choice_expr
  = elements:(expr %%% '|')

pub repeat_expr
  = expr '{' min:integer max:(',' integer?)? '}'

pub some_expr
  = expr '+'

pub many_expr
  = expr '*'

pub opt_expr
  = expr '?'

pub lookahead_expr
  = '&' expr

pub negative_lookahead_expr
  = '!' expr

pub list_expr
  = element:expr '%'+ separator:expr

# Rules

pub decorator
  = '@' name:ident

pub rule
  = decorator* 'pub'? 'extern'? 'token'? name:ident '=' expr

pub grammar
  = rule*
